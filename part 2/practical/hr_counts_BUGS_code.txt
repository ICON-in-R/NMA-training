#A) Fixed effects analysis

model{
  #Define Prior Distributions
  #On tx effect mean
  
  beta[1] <- 0
  
  for (tt in 2:nTx) {
    beta[tt] ~ dnorm(0, 1.0E-6)
  }

#On individual study baseline effect

for (ss in 1:nStudies) {
  alpha[ss] ~ dnorm(0,1.0E-6)
  }
  
#Fit data

#For hazard ratio reporting studies

for (ii in 1:LnObs) {
  Lmu[ii] <- alpha[Lstudy[ii]]*multi[ii] + beta[Ltx[ii]] - beta[Lbase[ii]]
  Lprec[ii] <- 1 / pow(Lse[ii], 2)
  Lmean[ii] ~ dnorm(Lmu[ii], Lprec[ii])
}

# For binary data reporting studies

for (ss in 1:BnObs) {
  logCumHaz[ss] <- alpha[Bstudy[ss]] + beta[Btx[ss]] - beta[Bbase[ss]]
  cumFail[ss] <- 1 - exp(-1 * exp(logCumHaz[ss]))
  Br[ss] ~ dbin(cumFail[ss], Bn[ss])
  }
  
# Calculate HRs

for (hh in 1:nTx) {
  hr[hh] <- exp(beta[hh])
  }
  
# Ranking plot
for (ll in 1:nTx) {
  for (mm in 1:nTx) {
    rk[ll,mm] <- equals(ranked(beta[],mm), beta[ll])
    }
  }
}

# Data
# Data set descriptors

list(LnObs = 5, BnObs = 8, nTx = 4, nStudies = 5)
# Log hazard ratio and log hazard data

Lstudy[] Ltx[] Lbase[] Lmean[] Lse[] multi[]
1 1 1  0     0.066 1
1 2 1  0.055 0.063 1
1 3 1 -0.154 0.070 1
1 4 1 -0.209 0.072 1
2 2 1 -0.276 0.203 0

# Binary data

Bstudy[] Btx[] Bbase[] Br[] Bn[]
3 3 1 1 229
3 1 1 1 227
4 2 1 4 374
4 3 1 3 372
4 4 1 2 358
4 1 1 7 361
5 3 1 1 554
5 1 1 2 270

# Initial values

list(
  alpha = c(-0.50,-0.50,-0.50,-0.50,-0.50),
  beta = c(NA,-0.5,-0.5,-0.5))

list(
  alpha = c(0.50, 0.50, 0.50, 0.50, 0.50),
  beta = c(NA, 0.5, 0.5, 0.5))

# B) Random effects analysis (changes required to incorporaterandom effect in bold)

model{

# Define Prior Distributions
# on random tx effect variance

sd ~ dunif(0,5)
reTau <- 2/pow(sd,2)

#On tx effect mean
beta[1] <- 0

for (tt in 2:nTx) {
  beta[tt] ~ dnorm(0, 1.0E-6)
}

#On individual study baseline effect

for (ss in 1:nStudies) {
  alpha[ss] ~ dnorm(0,1.0E-6)
}

#Define random effect

for (ss in 1:nStudies) {
  for (tt in 1:nTx) {
    re[ss, tt] ~ dnorm(0, reTau)
    }
}

#Fit data 
#For hazard ratio reporting studies

for (ii in 1:LnObs) {
  Lmu[ii] <- alpha[Lstudy[ii]]*multi[ii] + re[Lstudy[ii], Ltx[ii]] - re[Lstudy[ii], Lbase[ii]] + beta[Ltx[ii]] - beta[Lbase[ii]]
  Lprec[ii] <- 1 / pow(Lse[ii], 2)
  Lmean[ii] ~ dnorm(Lmu[ii], Lprec[ii])
  }

#For binary data reporting studies
for (ss in 1:BnObs) {
  logCumHaz[ss] <- alpha[Bstudy[ss]] + re[Bstudy[ss], Btx[ss]] - re[Bstudy[ss], Bbase[ss]] + beta[Btx[ss]] - beta [Bbase[ss]]
  cumFail[ss] <- 1 - exp(-1 * exp(logCumHaz[ss]))
  Br[ss] ~ dbin(cumFail[ss], Bn[ss])
  }
  
# Calculate HRs
for (hh in 2:nTx) {
  hr[hh] <- exp(beta[hh])
  }
  
# Ranking plot
for (ll in 1:nTx) {
  for (mm in 1:nTx) {
    rk[ll,mm] <- equals(ranked(beta[], mm), beta[ll])
    }
}
}

# Data as for fixed effects analysis ############################
# Initial values
list(
  alpha = c(-0.50,-0.50,-0.50,-0.50,-0.50),
  beta = c(NA,-0.5,-0.5,-0.5),
  sd = 0.1)
list(
  alpha = c(0.50, 0.50, 0.50, 0.50, 0.50),
  beta = c(NA, 0.5, 0.5, 0.5),
  sd = 1)

#C) Additional code required for data reported as median survival times

for (ii in 1:medianNObs) {
  medianMu[ii] <- alpha[medianStudy[ii]] + beta[medianTx[ii]] - beta[medianBase[ii]]
    prob[ii] <- exp(-median[ii] * exp(medianMu[ii]))
    medianR[ii] ~ dbin(prob[ii], medianN[ii])
}
